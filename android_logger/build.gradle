plugins{
    id 'com.android.library'
    id 'maven-publish'
}

def versionMajor = 1
def versionMinor = 4
def versionPatch = 4

def code = versionMajor * 10000 + versionMajor + 1000 * versionMinor + versionPatch

def versionName = "${versionMajor}.${versionMinor}.${versionPatch}"

android {
    compileSdk 33

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 33
        buildConfigField "String", "VERSION", "\"${versionName}\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    namespace 'apps.in.android_logger'
}

publishing {
    publications {
        bar(MavenPublication){
            groupId "apps.in"
            artifactId "logger"
            version "${versionName}"
            artifact ("$buildDir/outputs/aar/android_logger-release.aar")
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/apps-in/Logger")
            credentials {
                username = System.getenv("GITHUB_USER")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.appcompat:appcompat:1.6.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

